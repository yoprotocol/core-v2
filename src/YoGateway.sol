// SPDX-License-Identifier: MIT
pragma solidity 0.8.34;

import { Initializable } from "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import { IERC4626 } from "@openzeppelin/contracts/interfaces/IERC4626.sol";
import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { SafeERC20 } from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import { ReentrancyGuardTransient } from "@openzeppelin/contracts/utils/ReentrancyGuardTransient.sol";
import { IYoGateway } from "./interfaces/IYoGateway.sol";
import { IYoRegistry } from "./interfaces/IYoRegistry.sol";
import { IYoVault } from "./interfaces/IYoVault.sol";
import { Errors } from "./libraries/Errors.sol";

/// __     __    _____       _
/// \ \   / /   / ____|     | |
///  \ \_/ /__ | |  __  __ _| |_ _____      ____ _ _   _
///   \   / _ \| | |_ |/ _` | __/ _ \ \ /\ / / _` | | | |
///    | | (_) | |__| | (_| | ||  __/\ V  V / (_| | |_| |
///    |_|\___/ \_____|\__,_|\__\___| \_/\_/ \__,_|\__, |
///                                                 __/ |
///                                                |___/
/// @title YoGateway
/// @notice Single entrypoint for deposits and redemption requests across allow-listed YO ERC-4626 vaults.
///         - deposit(assets→shares) and redeem(shares→assets).
///         - Emits partnerId for attribution; does NOT manage partner registries or fees.
///         - Uses YoRegistry to manage allow-listed vaults.
///
/// Assumptions:
///  - redeem may be async (returns 0 when routed to the vault's requestRedeem). Gateway is oblivious; assets are
/// delivered by the vault.
///  - For third-party redemption (owner != sender), owner must approve the gateway to transfer shares.

contract YoGateway is Initializable, ReentrancyGuardTransient, IYoGateway {
    using SafeERC20 for IERC20;

    IYoRegistry public registry;

    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() {
        _disableInitializers();
    }

    function initialize(address _registry) public initializer {
        registry = IYoRegistry(_registry);
    }

    function deposit(
        address yoVault,
        uint256 assets,
        uint256 minSharesOut,
        address receiver,
        uint32 partnerId
    )
        external
        nonReentrant
        returns (uint256 sharesOut)
    {
        require(assets > 0, Errors.Gateway__ZeroAmount());
        require(receiver != address(0), Errors.Gateway__ZeroReceiver());
        require(registry.isYoVault(yoVault), Errors.Gateway__VaultNotAllowed());

        address asset = IERC4626(yoVault).asset();
        IERC20(asset).safeTransferFrom(msg.sender, address(this), assets);
        IERC20(asset).forceApprove(yoVault, assets);

        sharesOut = IERC4626(yoVault).deposit(assets, receiver);

        if (sharesOut < minSharesOut) {
            revert Errors.Gateway__InsufficientSharesOut(sharesOut, minSharesOut);
        }

        emit YoGatewayDeposit(partnerId, yoVault, msg.sender, receiver, assets, sharesOut);
    }

    function redeem(
        address yoVault,
        uint256 shares,
        uint256 minAssetsOut,
        address receiver,
        uint32 partnerId
    )
        external
        nonReentrant
        returns (uint256 assetsOrRequestId)
    {
        require(shares > 0, Errors.Gateway__ZeroAmount());
        require(receiver != address(0), Errors.Gateway__ZeroReceiver());
        require(registry.isYoVault(yoVault), Errors.Gateway__VaultNotAllowed());

        IERC20(yoVault).safeTransferFrom(msg.sender, address(this), shares);
        // `requestRedeem` returns `assetsWithFee` (gross of withdrawal fee) on the instant path,
        // but the receiver actually gets `assetsWithFee - fee`. `previewRedeem` on YoVault V3 is
        // overridden to return the net amount — that's what we compare against `minAssetsOut`.
        uint256 expectedNet = IERC4626(yoVault).previewRedeem(shares);
        assetsOrRequestId = IYoVault(yoVault).requestRedeem(shares, receiver, address(this));

        bool instant = assetsOrRequestId > 0;

        // Slippage guard against the *net* amount the receiver gets (fee-adjusted).
        if (instant && expectedNet < minAssetsOut) {
            revert Errors.Gateway__InsufficientAssetsOut(expectedNet, minAssetsOut);
        }

        emit YoGatewayRedeem(partnerId, yoVault, receiver, shares, assetsOrRequestId, instant);
    }

    function quoteConvertToShares(address yoVault, uint256 assets) external view returns (uint256) {
        require(registry.isYoVault(yoVault), Errors.Gateway__VaultNotAllowed());
        return IERC4626(yoVault).convertToShares(assets);
    }

    function quoteConvertToAssets(address yoVault, uint256 shares) external view returns (uint256) {
        require(registry.isYoVault(yoVault), Errors.Gateway__VaultNotAllowed());
        return IERC4626(yoVault).convertToAssets(shares);
    }

    function quotePreviewDeposit(address yoVault, uint256 assets) external view returns (uint256) {
        require(registry.isYoVault(yoVault), Errors.Gateway__VaultNotAllowed());
        return IERC4626(yoVault).previewDeposit(assets);
    }

    function quotePreviewRedeem(address yoVault, uint256 shares) external view returns (uint256) {
        require(registry.isYoVault(yoVault), Errors.Gateway__VaultNotAllowed());
        return IERC4626(yoVault).previewRedeem(shares);
    }

    function quotePreviewWithdraw(address yoVault, uint256 assets) external view returns (uint256) {
        require(registry.isYoVault(yoVault), Errors.Gateway__VaultNotAllowed());
        return IERC4626(yoVault).previewWithdraw(assets);
    }

    /// @notice Returns the current allowance of `owner` for shares of the given yoVault to this gateway.
    function getShareAllowance(address yoVault, address owner) external view returns (uint256) {
        require(registry.isYoVault(yoVault), Errors.Gateway__VaultNotAllowed());
        return IERC20(yoVault).allowance(owner, address(this));
    }

    /// @notice Returns the current allowance of `owner` for the underlying asset of the given yoVault to this gateway.
    function getAssetAllowance(address yoVault, address owner) external view returns (uint256) {
        require(registry.isYoVault(yoVault), Errors.Gateway__VaultNotAllowed());
        address asset = IERC4626(yoVault).asset();
        return IERC20(asset).allowance(owner, address(this));
    }
}
